// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl

/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load==1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid. Access to any address in 
 * the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the 
 * Screen and Keyboard chip specifications given in the book.
 */

CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    DMux(in=load, sel=address[14], a=r1, b=w1);
    RAM16K(in=in, load=r1, address=address[0..13], out=r2);
    DMux(in=w1, sel=address[13], a=s1, b=k1);
    Screen(in=in, load=s1, address=address[0..12], out=s2);
    Keyboard(out=k2);
    Or8Way(in=address[0..7], out=k3);
    Or8Way(in[0..4]=address[8..12], in[5..7]=false, out=k4);
    Or(a=k3, b=k4, out=k5);
    Mux16(a=k2, b=false, sel=k5, out=k6);
    Mux16(a=s2, b=k6, sel=address[13], out=w2);
    Mux16(a=r2, b=w2, sel=address[14], out=out);
}
